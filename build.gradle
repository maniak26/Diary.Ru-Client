buildscript {
    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0'
    }
}

apply plugin: 'com.android.application'

allprojects {
    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://jitpack.io" } // for saripaar and material-dialogs
        google()
    }
}

dependencies {
    compile 'pl.droidsonroids.gif:android-gif-drawable:1.2.7'                    // animated gif drawables
    compile 'com.afollestad.material-dialogs:core:0.9.4.5'                       // material design for dialogs
    //noinspection GradleCompatible
    compile 'com.android.support:design:27.0.0'                                  // material design controls
    compile 'com.j256.ormlite:ormlite-android:4.48'                              // ORM
    compile 'org.jsoup:jsoup:1.11.1'                                             // HTML parser
    compile 'org.mozilla:rhino:1.7.7.1'                                          // JS for CloudFlare solving
    compile 'com.squareup.okhttp3:okhttp:3.8.1'                                  // HTTP client
    compile 'com.squareup.okhttp3:okhttp-urlconnection:3.8.1'                    // cookie handling
    compile 'com.google.code.gson:gson:2.8.0'                                      // For Prostopleer Json (de)serializing
    compile 'com.github.ragunathjawahar:android-saripaar:android-saripaar-2.0.3' // text input validation with unreadable name
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 260
        versionName "2.6.0"
    }

    try {
        def proc = 'git rev-parse --short HEAD'.execute()
        def gitOutput = new StringBuilder('-')
        proc.waitForProcessOutput gitOutput, System.err
        def hash = gitOutput.toString().replace('\n', '').replace('\r', '')
        if(!hash.empty) {
            defaultConfig.versionName += hash
        }
    } catch (ignored) {
        println "Ensure that git executable is in your PATH to see extended version info in app"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {
            storeFile file("/home/adonai/devel/keystore/Adonai")
            storePassword "release"
            keyAlias "adonai"
            keyPassword "release"
        }
    }

    buildTypes {
        pro {
            debuggable false
            signingConfig signingConfigs.release
            applicationIdSuffix '.pro'
        }

        release {
            debuggable false
            signingConfig signingConfigs.release
        }
    }
}
